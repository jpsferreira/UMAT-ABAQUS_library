      SUBROUTINE AFFMATNETFIC(PKFIC,CMFIC,F,MF0,RW,FILPROPS,AFFPROPS,
     1                                  EFI,NOEL,DET,NDI)
C>    AFFINE NETWORK: 'FICTICIOUS' PK STRESS AND ELASTICITY TENSOR 
      IMPLICIT NONE
      INCLUDE 'param_umat.inc'
C
      INTEGER NDI,I1,J1,K1,L1,M1,NOEL
C
      DOUBLE PRECISION PKFIC(NDI,NDI),SFILFIC(NDI,NDI),
     1                  CMFIC(NDI,NDI,NDI,NDI),F(NDI,NDI),MF0(NWP,NDI),
     2                 RW(NWP),CFILFIC(NDI,NDI,NDI,NDI)
      DOUBLE PRECISION FILPROPS(6),AFFPROPS(4),MFI(NDI),MF0I(NDI)
      DOUBLE PRECISION DET,AUX,PI,LAMBDAI,DWI,DDWI,RWI,FI,FFI,RU0,DTIME
      DOUBLE PRECISION L,R0,MU0,B0,BETA,LAMBDA0,M,RHO
      DOUBLE PRECISION B,FRIC,FFMAX,ANG,EFI,FRAC(4)
      
C
C     FILAMENT
      L       = FILPROPS(1)
      R0      = FILPROPS(2)
      MU0     = FILPROPS(3)
      BETA    = FILPROPS(4)
      B0      = FILPROPS(5)
      LAMBDA0 = FILPROPS(6)
C     NETWORK
      M       = AFFPROPS(1)
      B       = AFFPROPS(2)
      FRIC    = AFFPROPS(3)
      FFMAX   = AFFPROPS(4)
C
      PI=FOUR*ATAN(ONE)
      AUX=M*(DET**(-ONE))*FOUR*PI
      CMFIC=ZERO
      PKFIC=ZERO
C      
       RHO=ONE   
C  
C        LOOP OVER THE INTEGRATION DIRECTIONS
      DO I1=1,NWP
C
       MFI=ZERO
       MF0I=ZERO
       DO J1=1,NDI
        MF0I(J1)=MF0(I1,J1)
       END DO
       RWI=RW(I1)
C
       CALL DEFFIL(LAMBDAI,MFI,MF0I,F,NDI)
C
       CALL FIL(FI,FFI,DWI,DDWI,LAMBDAI,LAMBDA0,L,R0,MU0,BETA,B0)
C
       CALL BANGLE(ANG,F,MFI,NOEL,NDI)
C      
       CALL DENSITY(RHO,ANG,B,EFI)
C       
       CALL SIGFILFIC(SFILFIC,RHO,LAMBDAI,DWI,MF0I,RWI,NDI)
C
       CALL CSFILFIC(CFILFIC,RHO,LAMBDAI,DWI,DDWI,MF0I,RWI,NDI)
C
       DO J1=1,NDI
        DO K1=1,NDI
         PKFIC(J1,K1)=PKFIC(J1,K1)+AUX*SFILFIC(J1,K1)
         DO L1=1,NDI
          DO M1=1,NDI
          CMFIC(J1,K1,L1,M1)=CMFIC(J1,K1,L1,M1)+AUX*CFILFIC(J1,K1,L1,M1)
          END DO
         END DO
        END DO
       END DO
C
      END DO
C
      RETURN
      END SUBROUTINE AFFMATNETFIC
